plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.1.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'uk.jamierocks.atlauncher'
archivesBaseName = 'api'
version = '1.0-SNAPSHOT'
description = "ATLauncher-API--Java - Representation of the ATLauncher API for Java."
ext.url = 'http://uno.jamierocks.uk/'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.3.1'
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    newLine = false
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('lexRepoUser') && project.hasProperty('lexRepoPass')) {
                // Lexteam Maven Repository
                repository(url: 'http://lex1.lexteam.xyz:8888/repository/internal/') {
                    authentication(userName: lexRepoUser, password: lexRepoPass)
                }

                snapshotRepository(url: 'http://lex1.lexteam.xyz:8888/repository/snapshots/') {
                    authentication(userName: lexRepoUser, password: lexRepoPass)
                }
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.archivesBaseName
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = "https://github.com/UnoLauncher/ATLauncher-API--Java"
                        connection = "scm:git:https://github.com/UnoLauncher/ATLauncher-API--Java.git"
                        developerConnection = "scm:git:git@github.com:UnoLauncher/ATLauncher-API--Java.git"
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/UnoLauncher/ATLauncher-API--Java/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
